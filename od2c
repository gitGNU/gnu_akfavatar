#!/usr/bin/awk -f

# Convert output of "od -t x1 <file>" into C-code
#
# Example-Usage:
# od -t x1 audio1.au | ./od2c name=audio1 > audio1.c
#
# Author: Andreas K. Foerster
# This script is free software; you have unlimited permission
# to copy, distribute and modify it.

NR == 1 {
  if (name == "") name = "data"
  print "const char " name "[] ="
  }

NF > 1 {
  printf "\t\""
  for (i = 2; i <= NF; i++)
    {
      printf "\\x%s", $i
      size++
    }
  print "\""
  }

END {
  print "\t;"
  print ""
  print "const int " name "_size = " size ";"
  }
