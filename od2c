#!/usr/bin/awk -f

# Convert output of "od -t x1 <file>" into C-code
#
# Example-Usage:
# od -t x1 -v audio1.au | ./od2c name=audio1 > audio1.c
#
# Copyright (c) 2009 Andreas K. Foerster
# Copying and distribution of this file, with or without modification,
# are permitted in any medium without royalty provided the copyright
# notice and this notice are preserved.  This file is offered as-is,
# without any warranty.

NR == 1 {
  if (name == "") name = "data"
  print "const char " name "[] ="
  }

NF > 1 {
  printf "\t\""
  for (i = 2; i <= NF; i++)
    {
      printf "\\x%s", $i
      size++
    }
  print "\""
  }

END {
  print "\t;"
  print ""
  print "const int " name "_size = " size ";"
  }
