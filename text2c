#!/usr/bin/awk -f

# Convert textfile into C-code (new awk)
#
# Example-Usage:
# ./text2c README > readme.c
# ./text2c name=info_txt README > readme.c
#
# Author: Andreas K. Foerster
# This script is free software; you have unlimited permission
# to copy, distribute and modify it.

NR == 1 {
  if (name == "" && FILENAME != "-") {
    name = FILENAME
    gsub (/\./, "_", name) # replace . with _
    }

  if (name == "") name = "text"

  print "const char " name "[] ="
}

{
  # mask backslashes - must be first
  gsub (/\\/, "\\\\")

  # mask quotation marks
  gsub (/\"/, "\\\"")

  # print with quotation marks and \n at the end
  print "\"" $0 "\\n\""
}

END { print ";" }
